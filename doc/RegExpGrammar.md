# Regular Expressions Grammar
The set of UniLex regular expressions is defined by the following context-free grammar (written in 
[ABNF](https://tools.ietf.org/html/rfc5234)).
```
REGEXP = PARTS
PARTS = PART [ALT_PARTS]
ALT_PARTS = ALT_SEPARATOR PARTS 
ALT_SEPARATOR = T_VERTICAL_LINE
PART = *ITEM
ITEM = ASSERT / ITEM_BODY [ITEM_QUANT]

ASSERT = ASSERT_LINE_START / ASSERT_LINE_FINISH
ASSERT_LINE_START = T_CIRCUMFLEX
ASSERT_LINE_FINISH = T_DOLLAR

ITEM_BODY = GROUP / CLASS / SYMBOL

GROUP = GROUP_START PARTS GROUP_END
GROUP_START = T_LEFT_BRACKET
GROUP_END = T_RIGHT_BRACKET

CLASS = CLASS_START [CLASS_INVERTOR] CLASS_BODY CLASS_END
CLASS_START = T_LEFT_SQUARE_BRACKET
CLASS_INVERTOR = T_CIRCUMFLEX
CLASS_BODY = FIRST_CLASS_ITEM *CLASS_ITEM
FIRST_CLASS_ITEM = (FIRST_UNESC_CLASS_SYMBOL / CLASS_SYMBOL) [RANGE]
CLASS_ITEM = CLASS_SYMBOL [RANGE]
FIRST_UNESC_CLASS_SYMBOL = T_RIGHT_SQUARE_BRACKET ; unescaped "]" is allowed as first symbol of a symbol class
CLASS_SYMBOL = ESC_CLASS_SYMBOL / UNESC_CLASS_SYMBOL
ESC_CLASS_SYMBOL = ESC CLASS_ESC_SEQUENCE
ESC = T_BACKSLASH
CLASS_ESC_SEQUENCE = ESC_SEQUENCE
UNESC_CLASS_SYMBOL = ; symbols available in class without escaping
    T_DOLLAR / T_LEFT_BRACKET / T_RIGHT_BRACKET / T_STAR / T_PLUS / T_COMMA / T_HYPHEN / T_QUESTION
    / T_LEFT_SQUARE_BRACKET / T_CIRCUMFLEX
    / T_LEFT_CURLY_BRACKET / T_VERTICAL_LINE / T_RIGHT_CURLY_BRACKET
    / T_CTL_ASCII / T_OTHER_HEX_LETTER / T_OTHER_ASCII_LETTER / T_PRINTABLE_ASCII_OTHER / T_OTHER_ASCII / T_NOT_ASCII   

RANGE = RANGE_SEPARATOR CLASS_SYMBOL
RANGE_SEPARATOR = T_HYPHEN
SYMBOL_CLASS_END = T_RIGHT_SQUARE_BRACKET

SYMBOL = SYMBOL_ANY / ESC_SYMBOL / UNESC_SYMBOL
SYMBOL_ANY = T_DOT

ESC_SYMBOL = ESC ESC_SEQUENCE
ESC_SEQUENCE = ESC_SIMPLE / ESC_SPECIAL / ESC_NON_PRINTABLE / ESC_GENERIC

ESC_SIMPLE = T_OTHER_ASCII_LETTER / T_OTHER_HEX_LETTER
    / T_DIGIT_OCT / T_DIGIT_DEC ; old-style octal or back-reference
        
ESC_SPECIAL = ; удаляет специальное значение из символа, если оно было
    T_DOLLAR / T_LEFT_BRACKET / T_RIGHT_BRACKET / T_STAR / T_PLUS / T_COMMA / T_HYPHEN / T_QUESTION
    / T_LEFT_SQUARE_BRACKET / T_BACKSLASH / T_RIGHT_SQUARE_BRACKET / T_CIRCUMFLEX
    / T_LEFT_CURLY_BRACKET / T_VERTICAL_LINE / T_RIGHT_CURLY_BRACKET
    / T_CTL_ASCII / T_OTHER_HEX_LETTER / T_OTHER_ASCII_LETTER / T_PRINTABLE_ASCII_OTHER / T_OTHER_ASCII / T_NOT_ASCII   

ESC_NON_PRINTABLE = ESC_CTL / ESC_OCT / EXC_HEX / ESC_UNICODE
ESC_CTL = ESC_CTL_MARKER ESC_CTL_CODE
ESC_CTL_MARKER = T_SMALL_C
ESC_CTL_CODE = PRINTABLE_ASCII

ESC_NUM_START = T_LEFT_CURLY_BRACKET
ESC_NUM_FINISH = T_RIGHT_CURLY_BRACKET
ESC_OCT = ESC_OCT_SHORT / ESC_OCT_LONG
ESC_OCT_SHORT = ESC_OCT_SHORT_MARKER ESC_OCT_SHORT_NUM ; old PCRE syntax with non-zero first digit is not supported
ESC_OCT_SHORT_NUM = 0*2OCT_DIGIT
ESC_OCT_SHORT_MARKER = T_DIGIT_ZERO
ESC_OCT_LONG = ESC_OCT_LONG_MARKER ESC_OCT_LONG_NUM
ESC_OCT_LONG_NUM = ESC_NUM_START OCT ESC_NUM_FINISH
ESC_OCT_LONG_MARKER = T_SMALL_O
ESC_HEX = ESC_HEX_MARKER ESC_HEX_NUM
ESC_HEX_MARKER = T_SMALL_X
ESC_HEX_NUM = ESC_HEX_SHORT_NUM / ESC_HEX_LONG_NUM
ESC_HEX_SHORT_NUM = 2HEX_DIGIT
ESC_HEX_LONG_NUM = ESC_NUM_START HEX ESC_NUM_FINISH
ESC_UNICODE = ESC_UNICODE_MARKER ESC_UNICODE_NUM
ESC_UNICODE_MARKER = T_SMALL_U
ESC_UNICODE_NUM = 4HEX_DIGIT

ESC_GENERIC = ESC_PROP / ESC_NOT_PROP
ESC_PROP = ESC_PROP_MARKER PROP
ESC_NOT_PROP = ESC_NOT_PROP_MARKER PROP
ESC_PROP_MARKER = T_SMALL_P
ESC_PROP_NOT_MARKER = T_CAPITAL_P
PROP = PROP_SHORT / PROP_FULL
PROP_SHORT = NOT_PROP_START
PROP_FULL = PROP_START PROP_NAME PROP_FINISH
PROP_START = T_LEFT_CURLY_BRACKET
PROP_FINISH = T_RIGHT_CURLY_BRACKET
PROP_NAME = *NOT_PROP_FINISH
NOT_PROP_START = ; any symbol but "{"
        T_DOLLAR / T_LEFT_BRACKET / T_RIGHT_BRACKET / T_STAR / T_PLUS / T_COMMA / T_HYPHEN / T_DOT / T_DIGIT_ZERO
        / T_DIGIT_OCT / T_DIGIT_DEC / T_QUESTION / T_CAPITAL_A / T_CAPITAL_B / T_CAPITAL_C / T_CAPITAL_D
        / T_CAPITAL_E / T_CAPITAL_F / T_CAPITAL_G / T_CAPITAL_H / T_CAPITAL_K / T_CAPITAL_N / T_CAPITAL_P / T_CAPITAL_Q
        / T_CAPITAL_R / T_CAPITAL_S / T_CAPITAL_V / T_CAPITAL_W / T_CAPITAL_X / T_CAPITAL_Z / T_LEFT_SQUARE_BRACKET
        / T_BACKSLASH / T_RIGHT_SQUARE_BRACKET / T_CIRCUMFLEX / T_SMALL_A / T_SMALL_B / T_SMALL_C / T_SMALL_D
        / T_SMALL_E / T_SMALL_F / T_SMALL_H / T_SMALL_N / T_SMALL_O / T_SMALL_R / T_SMALL_S / T_SMALL_T / T_SMALL_U
        / T_SMALL_V / T_SMALL_W / T_SMALL_X / T_SMALL_Z / T_VERTICAL_LINE
        / T_RIGHT_CURLY_BRACKET / T_CTL_ASCII / T_OTHER_HEX_LETTER / T_OTHER_ASCII_LETTER / T_PRINTABLE_ASCII_OTHER
        / T_OTHER_ASCII / T_NOT_ASCII        
NOT_PROP_FINISH = ; any symbol but "}"
        T_DOLLAR / T_LEFT_BRACKET / T_RIGHT_BRACKET / T_STAR / T_PLUS / T_COMMA / T_HYPHEN / T_DOT / T_DIGIT_ZERO
        / T_DIGIT_OCT / T_DIGIT_DEC / T_QUESTION / T_CAPITAL_A / T_CAPITAL_B / T_CAPITAL_C / T_CAPITAL_D
        / T_CAPITAL_E / T_CAPITAL_F / T_CAPITAL_G / T_CAPITAL_H / T_CAPITAL_K / T_CAPITAL_N / T_CAPITAL_P / T_CAPITAL_Q
        / T_CAPITAL_R / T_CAPITAL_S / T_CAPITAL_V / T_CAPITAL_W / T_CAPITAL_X / T_CAPITAL_Z / T_LEFT_SQUARE_BRACKET
        / T_BACKSLASH / T_RIGHT_SQUARE_BRACKET / T_CIRCUMFLEX / T_SMALL_A / T_SMALL_B / T_SMALL_C / T_SMALL_D
        / T_SMALL_E / T_SMALL_F / T_SMALL_H / T_SMALL_N / T_SMALL_O / T_SMALL_R / T_SMALL_S / T_SMALL_T / T_SMALL_U
        / T_SMALL_V / T_SMALL_W / T_SMALL_X / T_SMALL_Z / T_LEFT_CURLY_BRACKET / T_VERTICAL_LINE
        / T_CTL_ASCII / T_OTHER_HEX_LETTER / T_OTHER_ASCII_LETTER / T_PRINTABLE_ASCII_OTHER / T_OTHER_ASCII / T_NOT_ASCII        

UNESC_SYMBOL = ; available without escaping
        T_COMMA / T_HYPHEN / T_DIGIT_ZERO / T_DIGIT_OCT / T_DIGIT_DEC / T_CAPITAL_A / T_CAPITAL_B / T_CAPITAL_C
        / T_CAPITAL_D / T_CAPITAL_E / T_CAPITAL_F / T_CAPITAL_G / T_CAPITAL_H / T_CAPITAL_K / T_CAPITAL_N / T_CAPITAL_P
        / T_CAPITAL_Q / T_CAPITAL_R / T_CAPITAL_S / T_CAPITAL_V / T_CAPITAL_W / T_CAPITAL_X / T_CAPITAL_Z
        / T_RIGHT_SQUARE_BRACKET / T_CIRCUMFLEX / T_SMALL_A / T_SMALL_B / T_SMALL_C / T_SMALL_D / T_SMALL_E / T_SMALL_F
        / T_SMALL_H / T_SMALL_N / T_SMALL_O / T_SMALL_R / T_SMALL_S / T_SMALL_T / T_SMALL_U / T_SMALL_V / T_SMALL_W
        / T_SMALL_X / T_SMALL_Z / T_RIGHT_CURLY_BRACKET / T_CTL_ASCII
        / T_OTHER_HEX_LETTER / T_OTHER_ASCII_LETTER T_PRINTABLE_ASCII_OTHER / T_OTHER_ASCII / T_NOT_ASCII

ITEM_QUANT = ITEM_OPT / ITEM_QUANT_STAR / ITEM_QUANT_PLUS / LIMIT
ITEM_OPT = T_QUESTION
ITEM_QUANT_STAR = T_STAR
ITEM_QUANT_PLUS = T_PLUS
LIMIT = LIMIT_START MIN [LIMIT_SEPARATOR MAX] LIMIT_END
LIMIT_START = T_LEFT_CURLY_BRACKET
LIMIT_END = T_END_CURLY_BRACKET
LIMIT_SEPARATOR = T_COMMA
MIN = DEC
MAX = DEC

OCT_DIGIT = T_DIGIT_ZERO / T_DIGIT_OCT
OCT = 1*OCT_DIGIT
DEC_DIGIT = T_DIGIT_ZERO / T_DIGIT_OCT / T_DIGIT_DEC
DEC = 1*DEC_DIGIT
HEX_DIGIT = T_DIGIT_ZERO / T_DIGIT_OCT / T_DIGIT_DEC / T_SMALL_C / T_OTHER_HEX_LETTER
HEX = 1*HEX_DIGIT

META_CHAR = 
        T_DOLLAR / T_LEFT_BRACKET / T_RIGHT_BRACKET / T_STAR / T_PLUS / T_COMMA / T_HYPHEN / T_DOT / T_QUESTION
        / T_LEFT_SQUARE_BRACKET / T_BACKSLASH / T_RIGHT_SQUARE_BRACKET / T_CIRCUMFLEX / T_LEFT_CURLY_BRACKET
        / T_VERTICAL_LINE / T_RIGHT_CURLY_BRACKET
ASCII_LETTER = T_CAPITAL_P / T_SMALL_C / T_SMALL_O / T_SMALL_P / T_SMALL_U / T_SMALL_X 
        / T_OTHER_ASCII_LETTER / T_OTHER_HEX_LETTER
PRINTABLE_ASCII = META_CHAR / DEC_DIGIT / ASCII_LETTER / T_PRINTABLE_ASCII_OTHER

T_CTL_ASCII = %x00-%x1F

T_DOLLAR = %x24 ; "$"
T_LEFT_BRACKET = %x28 ; "("
T_RIGHT_BRACKET = %x29 ; ")"
T_STAR = %x2A ; "*"
T_PLUS = %x2B ; "+"
T_COMMA = %x2C ; ","
T_HYPHEN = %x2D ; "-"
T_DOT = %x2E ; "."
T_DIGIT_ZERO = %x30 ; "0"
T_DIGIT_OCT = %x31-37 ; "1" - "7"
T_DIGIT_DEC = %x38-39 ; "8" - "9"
T_QUESTION = %x3F ; "?"
T_CAPITAL_P = %x50 ; "P"
T_LEFT_SQUARE_BRACKET = %x5B ; "["
T_BACKSLASH = %x5C ; "\"
T_RIGHT_SQUARE_BRACKET = %x5D ; "]"
T_CIRCUMFLEX = %x5E ; "^"
T_SMALL_C = %x63 ; "c"
T_SMALL_O = %x6F ; "o"
T_SMALL_P = %x70 ; "p"
T_SMALL_U = %x75 ; "u"
T_SMALL_X = %x78 ; "x"
T_LEFT_CURLY_BRACKET = %x7B ; "{"
T_VERTICAL_LINE = %x7C ; "|"
T_RIGHT_CURLY_BRACKET = %x7D ; "}"

T_OTHER_HEX_LETTER = %41-46 / %x61-62 / %x64-66 ; all hex digits but "c"
T_OTHER_ASCII_LETTER = %x47-4F / %x51-5A / %x67-6E / %x72-74 / %x76-77 / %x79-7A ; all remaining ASCII letters
T_PRINTABLE_ASCII_OTHER = %x20-23 / %x25-27 / %x2F / %x3A-3E / %x40 / 5F-60 / %x7E
T_OTHER_ASCII = %x7F
T_NOT_ASCII = %x80-10FFFF        
```